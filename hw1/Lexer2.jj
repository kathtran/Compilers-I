//----------------------------------------------------------------------
// This is an implementation of a lexer using JavaCC. (For CS321 HW1)
//----------------------------------------------------------------------
//
// Kathleen Tran
// Fall 2015
//

// A lexer for recognizing miniJava. (JavaCC version)
//
PARSER_BEGIN(Lexer2)
public class Lexer2 {}
PARSER_END(Lexer2)

// Token Definitions
//
SKIP:   { <SPACE:  " " | "\t" | "\n" | "\r" > }        // whitespace chars

SPECIAL_TOKEN: { <SL_COMMENT: "//" (~["\n"])*> }
MORE:	{ "/*": COMMENT }
<COMMENT> MORE: { <~[]> }
<COMMENT> SPECIAL_TOKEN: { <BLK_COMMENT: "*/">: DEFAULT }

// keywords
TOKEN:  {
<CLASS:         "class">
| <EXTENDS:     "extends">
| <STATIC:      "static">
| <PUBLIC:      "public">
| <MAIN:        "main">
| <VOID:        "void">
| <BOOLEAN:     "boolean">
| <INT:         "int">
| <DOUBLE:      "double">
| <STRING:      "String">
| <TRUE:        "true">
| <FALSE:       "false">
| <NEW:         "new">
| <THIS:        "this">
| <IF:          "if">
| <ELSE:        "else">
| <WHILE:       "while">
| <RETURN:      "return">
| <SYSTEM:      "System">
| <OUT:         "out">
| <PRINTLN:     "println">
}

TOKEN:  {
<#DIGIT:	    ["0"-"9"]>
| <#LETTER:	    ["a"-"z"]|["A"-"Z"]>
}
TOKEN:  {
<#INTEGER:      (<DIGIT>)+>
| <#OCTAL:       "0"(["0"-"7"])+>
| <#HEXADECIMAL: "0"["x","X"](["0"-"9", "a"-"f", "A"-"F"])+>
}

TOKEN:  { <ID:          (<LETTER>)+((<DIGIT>)|(<LETTER>))*> }

// literals
TOKEN:  { <INTLIT:      ((<INTEGER>)|(<OCTAL>)|(<HEXADECIMAL>))>
        { try { Integer.parseInt(matchedToken.image); }
          catch (Exception e) {
              throw new TokenMgrError("Lexical Error at column " +
              matchedToken.beginColumn + ": Invalid integer literal " +
              matchedToken.image, 0);
          }
        } 
}
TOKEN:  { <DBLLIT:      ((<DIGIT>)*"."(<DIGIT>)+)|((<DIGIT>)+"."(<DIGIT>)*)> }
TOKEN:  { <STRLIT:      "\""(~["\n","\r","\""])*"\""> }

// operators
TOKEN:  {
<ADD:           "+">
| <SUB:         "-">
| <MUL:         "*">
| <DIV:         "/">
| <AND:         "&&">
| <OR:          "||">
| <NOT:         "!">
| <EQ:          "==">
| <NE:          "!=">
| <LT:          "<">
| <LE:          "<=">
| <GT:          ">">
| <GE:          ">=">
}

// delimiters
TOKEN:  {
<ASSGN:         "=">
| <SEMI:        ";">
| <COMMA:       ",">
| <DOT:         ".">
| <LPAREN:      "(">
| <RPAREN:      ")">
| <LBRAC:       "[">
| <RBRAC:       "]">
| <LCURLY:      "{">
| <RCURLY:      "}">
}

SPECIAL_TOKEN: {
    <~[]> { if (true) throw new TokenMgrError("Illegal character at column " +
                        matchedToken.beginColumn + ": " +
                        matchedToken.image, 0); }
}
