// This is supporting software for CS321/CS322 Compilers and Language Design.
// Copyright (c) Portland State University.
//---------------------------------------------------------------------------
// For CS321 F'15 (J. Li).
//

// Expr2.jj - AST generation for the expression grammar:
//
// (E2) E -> id F
//   	F -> (+|-) id F
//   	F ->             // epsilon
//
// This grammar is equivalent to E1 (in Expr1.jj), but uses strict BNF notation.
//
//
PARSER_BEGIN(Expr2)
import ast.*;

public class Expr2 {
  public static void main(String [] args) throws Exception {
    Ast.Exp e = new Expr2(System.in).E0();
    System.out.println(e);
  }
}
PARSER_END(Expr2)

SKIP : { " " | "\t" | "\r" | "\n" }

TOKEN : 
{
  <#DIGIT:  ["0"-"9"]>
| <#LETTER: ["A"-"Z"]|["a"-"z"]>
| <ID:      <LETTER> (<LETTER>|<DIGIT>)*> 
}

// E0 -> E <EOF>
//
Ast.Exp E0(): 
{ Ast.Exp e; } 
{ 
  e=E() <EOF> { return e; }
}

// E -> Id F
//
Ast.Exp E():
{ Ast.Id id; Ast.Exp e; }
{
  id=Id() e=F(id) { return e==null ? id : e; }
}

// F -> [Op Id F]
//
Ast.Exp F(Ast.Exp e0):
{ Ast.BOP op; Ast.Id id; Ast.Exp e=null; }
{
  [ op=Op() id=Id() { e0 = new Ast.Binop(op,e0,id); } e=F(e0) ]
  { return e==null ? e0 : e; }
}

// Id -> <ID>
//
Ast.Id Id():
{ Token tkn; }
{
  tkn=<ID> { return new Ast.Id(tkn.image); }
}

// Op -> "+"|"-"
//
Ast.BOP Op(): {}
{
  "+" { return Ast.BOP.ADD; } 
| "-" { return Ast.BOP.SUB; }
}
